program → declaration* EOF ;

declaration → funDecl | varDecl | statement ;

statement → exprStmt | returnStmt | forStmt | ifStmt | printStmt | whileStmt | breakStmt | continueStmt | block ;

exprStmt → expression ";" ;
printStmt → "print" expression ";" ;
returnStmt → "return" expression? ";" ;
ifStmt → "if" "(" expression ")" statement ( "else" statement )? ;
whileStmt → "while" "(" expression ")" statement ;
forStmt → "for" "(" ( varDecl | exprStmt | ";" ) expression? ";" expression? ")" statement ;
block → "{" declaration* "}" ;
breakStmt → "break" ";" ;
continueStmt → "continue" ";" ;

varDecl → "var" IDENTIFIER ( "=" expression )? ";" ;
funDecl → "fun" function ;
function → IDENTIFIER "(" parameters? ")" block ;
parameters → IDENTIFIER ( "," IDENTIFIER )* ;

expression → assignment ;
assignment → ( call "." )? IDENTIFIER "=" assignment | logic_or ;
logic_or → logic_and ( "or" logic_and )* ;
logic_and → equality ( "and" equality )* ;
equality → comparison ( ( "!=" | "==" ) comparison )* ;
comparison → term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term → factor ( ( "-" | "+" ) factor )* ;
factor → unary ( ( "/" | "*" ) unary )* ;
unary → ( "!" | "-" ) unary | call ;
call → primary ( "(" arguments? ")" | "." IDENTIFIER )* ;
primary → "true" | "false" | "nil" | "nox" | NUMBER | STRING | "(" expression ")" | IDENTIFIER ;

arguments → expression ( "," expression )* ;

NUMBER → DIGIT+ ( "." DIGIT+ )? ;
STRING → "\"" <any char except "\"">* "\"" ;
IDENTIFIER → ALPHA ( ALPHA | DIGIT )* ;
ALPHA → "a" ... "z" | "A" ... "Z" | "_" ;
DIGIT → "0" ... "9" ;
